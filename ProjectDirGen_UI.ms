/*
	-------------------------------------------------------
	Script	: 	Project Directory Generator v3.5 [GUI]
	Date	:	Nov 2018
	-------------------------------------------------------
	Notes	:	Creates new project. 
	Sets up directory structure fo new project and
	optionally creates empty directory structures 
	for new shots. (It does not set up scene files)

	TO-DO : 
	
	Version updates:
		v3.3:	- Set new folder permissions
		v3.4:	- Add button to open "ShotMaker"
		v3.5:	- Use xml file (prjDirTemplate.xml) to create folder structure
*/

(
	
------------------------------------------
--		Include Libraries / Files	--
------------------------------------------
--
--
------------------------------------------
	
-- initialise variables
prjPath = ""
prjName = ""
scanPrjList = #()

fn InvalidName _msg =
(
	MessageBox _msg title:"Invalid project name"
)

-- extract existing project names 
fn ScanPrj = 
(
	scanPrjList = getDirectories (prjDir + "*")
	
	if scanPrjList.count != 0 then
	(
		for i = 1 to scanPrjList.count do
		(
			scanPrjList[i] = substring scanPrjList[i] (prjDir.count + 1) scanPrjList[i].count
			scanPrjList[i] = trimright scanPrjList[i] @"\"
		)
	)
	else scanPrjList[1] = "<empty>"
)

rollout _newShots "New Shots"
(
	--dropdownlist _prjList 
	label _lblPrj "Project: " align:#right across:2
	label _lblTitle align:#left
	label _lbl "Create directory structure for..." align:#left pos:[13, 36]
	edittext _seqInput "Seq #" text:"000" width:65 height:17 across:2
	edittext _shotInput "Num #" text:"000" width:66 height:17
	label _space
	button _doShot "Create" height:22 width:50 across:2
	button _openShotMaker "ShotMaker" height:22
	
	on _doShot pressed do
	(
		if isNumeric _seqInput.text and isNumeric _shotInput.text then
			if _seqInput.text.count != 3 or _shotInput.text.count != 3 then messageBox ("Invalid shot format")
			else
			(
				shot = _seqInput.text + "_" + _shotInput.text
				-- create shot directory structure
				for disDir in disGrp do
					makedir (prjPath + @"\shots\" + shot + @"\" + disDir)
				SetPermissions prjPath
			)
		else messageBox ("Invalid shot format")
	)
)
			
rollout _newPrj "New Project"
(
	edittext _prjName "Name" text:"NewProject" --width:110 across:2
	edittext _prjCode "Code " text:"--" --width:55 align:#right
	label _lblNotes "Notes:" align:#left
	edittext _prjNotes height:50 align:#center
	checkbox _openDir "Open project directory" align:#center
	checkbox _createPic "Create icon..." align:#center
	button _go "--> Create <--"
	
	on _prjName entered _newPrjName do		
	(
		if _newPrjName != "" then 
		(
			ScanPrj()
			
			-- check if project name exists
			for i in scanPrjList do
				if i == _newPrjName then InvalidName "This project name already exists !"
			
			-- suggest auto-generated project code
			_prjCode.text = toUpper (substring _newPrjName 1 2)
		)
	)
	
	--on _prjCode entered _code do
		-- make sure that project code is 2 letter only and capitalised 
		--_prjCode.text = toUpper (substring _code 1 2)
	
	on _go pressed do
	(
		if (_prjName.text != "") and (_prjCode.text != "--") then
		(
			valid = true
			for i = 1 to _prjName.text.count do
			(
				if _prjName.text[i] == "_" or _prjName.text[i] == "." then 
				(
					InvalidName "Invalid character(s) '_' or '.'"
					valid = false
				)
			)
			if valid then
			(
				prjCode = _prjCode.text
				prjName = _prjName.text
				prjPath = prjDir + prjName
				
				xmlDoc = dotNetObject "system.xml.xmlDocument"
				xmlDoc.load (devDir + @"etc\prjDirTemplate.xml")
				
				makeDirFromXML xmlDoc.DocumentElement location:prjPath
				
				DateTime()
				notesFileOut = prjPath + @"\" + prjName + ".txt"
				notesFile = createFile notesFileOut --mode:"wt"
				format _prjNotes.text to:notesFile
				format ("\n\n- Project created on " + timestring) to:notesFile
				format "\n- Automatically generated by ProjectDirGen_v003\n" to:notesFile
				format "-----------------------------------------------\n- (c) Nikolaos Maragkos - 2015\n\n" to:notesFile
				close notesFile
				
				if _openDir.checked then ShowDir prjPath
					
				if _createPic.checked then ShowDir (devDir + @"etc\prjTemplate.psd")

				_newShots._lblTitle.text = prjName
				_newPrj.open = false
				_newShots.open = true
			)
		)
		else InvalidName "Invalid project name or code !"
	)
)

fn prjDirGen =
(	
	try (closeRolloutFloater prjSetup) catch()
	
	prjSetup = newRolloutFloater "Project SetUp" 210 220
	addRollout _newPrj prjSetup  
	addRollout _newShots prjSetup rolledup:true
)

)